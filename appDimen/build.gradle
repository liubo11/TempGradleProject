apply plugin: 'com.android.application'

def flavName = ['flv1':'flv1Name_',
               'flv2':'flv2Name_']

android {
    compileSdkVersion rootProject.projectConfig.compileSdkVersion

    signingConfigs {
        debug {
        }
        release {
            storeFile file("keystore/lbReleaseAlpha1.jks")
            storePassword "123456"
            keyAlias "com.lb.release.alpha1"
            keyPassword "123456"
        }
    }

    defaultConfig {
        applicationId "com.lb.collection"
        minSdkVersion rootProject.projectConfig.minSdkVersion
        targetSdkVersion rootProject.projectConfig.targetSdkVersion
        versionCode rootProject.projectVersion.versionCode
        versionName rootProject.projectVersion.versionName

        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def apkTypeName = "-"

            if (flavName.containsKey(variant.flavorName)) {
                apkTypeName = flavName[variant.flavorName]
            }

            if (variant.buildType.name == 'release') {
                //修改目录
                File outputDirectory = file("releaseApk")
                if (!outputDirectory.exists() || !outputDirectory.isDirectory()) {
                    outputDirectory.mkdir()
                }
                variant.getPackageApplication().outputDirectory = outputDirectory
                outputFileName = "${apkTypeName}${releaseTime()}-${variant.versionName}.${variant.versionCode}.apk"
            }
        }
    }
    
    lintOptions {
        //出现错误不终止构建
        abortOnError false
        //不输入日志
        htmlReport false
        //不输入日志
        xmlReport false
        //release 时检测fatal等级
        checkReleaseBuilds true
        disable 'MissingTranslation'
    }

    flavorDimensions "fd"
    productFlavors {
        flv1 {
            applicationId       "com.lb.fd1"
            minSdkVersion rootProject.projectConfig.minSdkVersion
            targetSdkVersion rootProject.projectConfig.targetSdkVersion
            versionCode rootProject.projectVersion.versionCode
            versionName rootProject.projectVersion.versionName
            dimension           "fd"
            signingConfig signingConfigs.release
        }
        flv2 {
            applicationId       "com.lb.fd2"
            minSdkVersion rootProject.projectConfig.minSdkVersion
            targetSdkVersion rootProject.projectConfig.targetSdkVersion
            versionCode rootProject.projectVersion.versionCode
            versionName rootProject.projectVersion.versionName
            dimension           "fd"
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
